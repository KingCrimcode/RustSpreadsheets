WHITESPACE = _{ " " }

formula            = _{ SOI ~ "=" ~ expr ~ EOI }
  expr             =  { atom ~ ( operator ~ atom )* }
    atom           = _{ prefix? ~ primary | func | "(" ~ expr ~ ")" }
      prefix       = _{ neg }
        neg        =  { "-" }
      primary      = _{ number | cell_ref }
        number     = @{ ASCII_DIGIT+ ~ ( "." ~ ASCII_DIGIT+ )? }
        cell_ref   = @{ col_ref ~ row_ref }
          col_ref  = _{ ASCII_ALPHA+ }
          row_ref  = _{ ASCII_DIGIT+ }
      func         =  { func_name ~ "(" ~ func_args? ~ ")" }
        func_name  = @{ ASCII_ALPHA+ }
        func_args  =  { func_arg ~ ( "," ~ func_arg )* }
          func_arg = _{ expr }
    operator       = _{ add | sub | mul | div | pow }
      add          =  { "+" }
      sub          =  { "-" }
      mul          =  { "*" }
      div          =  { "/" }
      pow          =  { "^" }
